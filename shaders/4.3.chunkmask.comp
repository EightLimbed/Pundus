#version 430 core

layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in; // 64 local threads is apparently sweet spot

layout(std430, binding = 0) buffer BlockData {
    uint chunkMask[524288];
    uint blockData[];
};

// morton encoding/decoding
uint part1by2(uint x) {
    x &= 0x000003FFu;
    x = (x | (x << 16)) & 0x030000FFu;
    x = (x | (x << 8))  & 0x0300F00Fu;
    x = (x | (x << 4))  & 0x030C30C3u;
    x = (x | (x << 2))  & 0x09249249u;
    return x;
}

uint morton3D(uvec3 p) {
    return part1by2(p.x) | (part1by2(p.y) << 1) | (part1by2(p.z) << 2);
}

void main() {
    uvec3 id = gl_GlobalInvocationID;
    // morton
    uint m = morton3D(id);
    // indexing for bitpacking
    uint idx = m >> 5u; 
    uint bit = m & 31u;
    // every two uints checked as group to see if empty.
    uint nm = m*4u;
    bool empty = true;
    for (uint i = 0u; i < 4u; i++) { // for loop for possibility of bigger chunks
        if (blockData[nm+i] != 0u) {
            empty = false;
            break;
        }
    }
    if (empty) {
        atomicOr(chunkMask[idx], 1u << bit);
    } else {
        atomicAnd(chunkMask[idx], ~(1u << bit));
    }
}