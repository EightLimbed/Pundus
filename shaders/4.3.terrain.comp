#version 430 core

layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in; //64 local threads is apparently sweet spot

layout(std430, binding = 0) buffer VoxelData {
    uint bitCloud[1024];
};

// helper functions
vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec2 mod289(vec2 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec3 permute(vec3 x) { return mod289(((x*34.0)+1.0)*x); }

//
// Description : GLSL 2D simplex noise function
//      Author : Ian McEwan, Ashima Arts
//  Maintainer : ijm
//     Lastmod : 20110822 (ijm)
//     License :
//  Copyright (C) 2011 Ashima Arts. All rights reserved.
//  Distributed under the MIT License. See LICENSE file.
//  https://github.com/ashima/webgl-noise
//
float noise2D(vec2 v) {

    // Precompute values for skewed triangular grid
    const vec4 C = vec4(0.211324865405187,
                        // (3.0-sqrt(3.0))/6.0
                        0.366025403784439,
                        // 0.5*(sqrt(3.0)-1.0)
                        -0.577350269189626,
                        // -1.0 + 2.0 * C.x
                        0.024390243902439);
                        // 1.0 / 41.0

    // First corner (x0)
    vec2 i  = floor(v + dot(v, C.yy));
    vec2 x0 = v - i + dot(i, C.xx);

    // Other two corners (x1, x2)
    vec2 i1 = vec2(0.0);
    i1 = (x0.x > x0.y)? vec2(1.0, 0.0):vec2(0.0, 1.0);
    vec2 x1 = x0.xy + C.xx - i1;
    vec2 x2 = x0.xy + C.zz;

    // Do some permutations to avoid
    // truncation effects in permutation
    i = mod289(i);
    vec3 p = permute(
            permute( i.y + vec3(0.0, i1.y, 1.0))
                + i.x + vec3(0.0, i1.x, 1.0 ));

    vec3 m = max(0.5 - vec3(
                        dot(x0,x0),
                        dot(x1,x1),
                        dot(x2,x2)
                        ), 0.0);

    m = m*m ;
    m = m*m ;

    // Gradients:
    //  41 pts uniformly over a line, mapped onto a diamond
    //  The ring size 17*17 = 289 is close to a multiple
    //      of 41 (41*7 = 287)

    vec3 x = 2.0 * fract(p * C.www) - 1.0;
    vec3 h = abs(x) - 0.5;
    vec3 ox = floor(x + 0.5);
    vec3 a0 = x - ox;

    // Normalise gradients implicitly by scaling m
    // Approximation of: m *= inversesqrt(a0*a0 + h*h);
    m *= 1.79284291400159 - 0.85373472095314 * (a0*a0+h*h);

    // Compute final noise value at P
    vec3 g = vec3(0.0);
    g.x  = a0.x  * x0.x  + h.x  * x0.y;
    g.yz = a0.yz * vec2(x1.x,x2.x) + h.yz * vec2(x1.y,x2.y);
    return 130.0 * dot(m, g);
}

// morton encoding/decoding
uint part1by2(uint x) {
    x &= 0x000003FFu; // 10 bits
    x = (x | (x << 16)) & 0x030000FFu;
    x = (x | (x << 8))  & 0x0300F00Fu;
    x = (x | (x << 4))  & 0x030C30C3u;
    x = (x | (x << 2))  & 0x09249249u;
    return x;
}

uint morton3D(uvec3 id) {
    return part1by2(id.x) | (part1by2(id.y) << 1) | (part1by2(id.z) << 2);
}

uint compact1by2(uint x) {
    x &= 0x09249249u;
    x = (x ^ (x >> 2)) & 0x030C30C3u;
    x = (x ^ (x >> 4)) & 0x0300F00Fu;
    x = (x ^ (x >> 8)) & 0x030000FFu;
    x = (x ^ (x >> 16)) & 0x000003FFu;
    return x;
}

uvec3 mortonDecode3D(uint m) {
    uint x = compact1by2(m);
    uint y = compact1by2(m >> 1);
    uint z = compact1by2(m >> 2);
    return uvec3(x, y, z);
}

void setVoxel(uvec3 p, bool filled) {
    uint m = morton3D(p);
    uint idx = m >> 5;
    uint bit = m & 31u;
    if(filled) {
        atomicOr(bitCloud[idx], 1u << bit);
    } else {
        atomicAnd(bitCloud[idx], ~(1u << bit));
    }
}

void main() {
  uvec3 id = gl_GlobalInvocationID;
  //sets voxel based on wether or not it is filled
  float height = (noise2D(vec2(id.x,id.z)/32.0) + 1.0) * 0.5 * 32.0;
  bool filled = float(id.y) < height;
  setVoxel(id, filled);
}